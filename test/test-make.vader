Execute (typevim#make#Class: Rudimentary Class):
  let g:obj = TestBase#New(3.14)
  call typevim#ensure#IsType(g:obj, 'TestBase')
  AssertEqual ['TestBase'], g:obj[typevim#attribute#TYPE()]
  AssertEqual 0, g:obj.Destroy(), 'Object not initialized with default destructor.'
  Assert !has_key(g:obj, typevim#attribute#DESTRUCTOR_LIST()),
      \ printf('Had a destructor list when it should not have: %s',
      \ typevim#object#ShallowPrint(g:obj))
Then (Invoke Functions):
  AssertEqual 3.14, g:obj.GetVal()
  call g:obj.SetVal('foo')
  AssertEqual 'foo', g:obj.GetVal()

Execute (typevim#make#Class: Class with Simple Destructor):
  let g:obj = TestBase#New(3.14, { -> 1})
  call typevim#ensure#IsType(g:obj, 'TestBase')
  AssertEqual ['TestBase'], g:obj[typevim#attribute#TYPE()]
  AssertEqual 1, g:obj.Destroy(),
      \ 'Object not initialized with given destructor.'
  Assert !has_key(g:obj, typevim#attribute#DESTRUCTOR_LIST()),
      \ printf('Had a destructor list when it should not have: %s',
      \ typevim#object#ShallowPrint(g:obj))

Execute (typevim#make#Derived: Derived Class, No Base Dtor):
  let g:der = TestDerived#New(0, 0, 0)
  call typevim#ensure#IsType(g:der, 'TestBase')
  call typevim#ensure#IsType(g:der, 'TestDerived')
  Assert !has_key(g:der, typevim#attribute#DESTRUCTOR_LIST()),
      \ printf('Had a destructor list when it should not have: %s',
      \ typevim#object#ShallowPrint(g:der, 2))
  AssertEqual 'overridden', g:der.GetVal()

Execute (typevim#make#Derived: Derived Class, With Base Dtor):
  let g:der = TestDerived#New(1, 0, 0)
  call typevim#ensure#IsType(g:der, 'TestBase')
  call typevim#ensure#IsType(g:der, 'TestDerived')
  AssertEqual ['TestBase', 'TestDerived'], g:der[typevim#attribute#TYPE()]
  Assert has_key(g:der, typevim#attribute#DESTRUCTOR_LIST()),
      \ printf('Did not have a destructor list when it should have: %s',
      \ typevim#object#ShallowPrint(g:der, 2))
  AssertEqual 2, len(g:der[typevim#attribute#DESTRUCTOR_LIST()])

Then (Check Error Messages from Default Virtual Function):
  AssertThrows call g:der.SmallVirtual()
  " Not enough arguments...
  AssertNotEqual -1, match(g:vader_exception, 'E119'),
      \ 'Threw wrong exception, should be E119: '.g:vader_exception

  AssertThrows call g:der.SmallVirtual('foo', 'boo')
  " Too many arguments for...
  AssertNotEqual -1, match(g:vader_exception, 'E118'),
      \ 'Threw wrong exception, should be E118: '.g:vader_exception

  AssertThrows call g:der.MediumVirtual('foo', 'boo')
  AssertNotEqual -1, match(g:vader_exception, '^ERROR(NotImplemented)'),
      \ 'Threw wrong exception, should be NotImplemented: '.g:vader_exception

  " specify optional
  AssertThrows call g:der.MediumVirtual('foo', 'boo', 'roo')
  AssertNotEqual -1, match(g:vader_exception, '^ERROR(NotImplemented)'),
      \ 'Threw wrong exception, should be NotImplemented: '.g:vader_exception

  " specify too many optional
  AssertThrows call g:der.MediumVirtual('foo', 'boo', 'roo', 'aaaaah')
  AssertNotEqual -1, match(g:vader_exception, '^ERROR(InvalidArguments)'),
      \ 'Threw wrong exception, should be InvalidArguments: '.g:vader_exception

  " specify too many optional
  AssertThrows call g:der.BigVirtual('foo', 'boo', 'roo', 'aaaaah')
  AssertNotEqual -1, match(g:vader_exception, '^ERROR(NotImplemented)'),
      \ 'Threw wrong exception, should be NotImplemented: '.g:vader_exception
